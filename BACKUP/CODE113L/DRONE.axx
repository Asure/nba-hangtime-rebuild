	.file	"drone.asm"
	.title	"basketball drone code"
	.width	132
	.option	b,d,l,t
	.mnolist


	.include	"mproc.equ"		;Mproc equates
	.include	"disp.equ"		;Display proc equates
	.include	"gsp.equ"		;Gsp asm equates
	.include	"sys.equ"
	.include	"audit.equ"
	.include	"macros.hdr"		;Macros
	.include	"world.equ"		;Court-world defs
	.include	"game.equ"
	.asg		0,SEQT
	.include	"plyr.equ"





	.ref	plyrobj_t,plyrproc_t
	.ref	ballobj_p
	.ref	ballpnum,ballpnumshot
	.ref	ballnumscored,ballpnumscored
	.ref	balltmshotcnt,balltmscored
	.ref	plyr_onfire
	.ref	seekdirdist_obxz128

	.ref	game_time,gmqrtr
	.ref	shotimer

	.ref	team1,team2

	.ref	PCNT
	.ref	RNDPER
	.ref	PSTATUS
	.ref	GET_ADJ





	.bss	drnzzcnt	,16	;Drone zigzag mode cntdn
	.bss	drnzzmode	,16	;Drone zigzag mode (0-?)
	BSSX	drone2on	,16	;!0=Use drone version2 code

	BSSX	dronesmrt	,16	;Bit 0-3 if 1 = Smarter drone




BV5	equ	01040H-080H+042H*6

NOPUSHSTEAL	equ	0	;!0=No push or stealing
ONLY3		equ	0	;!0=Only shoot 3's

	.text




 SUBR	drone_main





	move	@ballpnum,a14
	jrn	drone_chaseball		;No owner?


	move	*a13(plyr_ownball),a1
	jrz	drone_defense		;We don't have ball?
	jrn	drone_offwoball		;Teammate has ball?



	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	yqbhtmdrone		;Teammate is a drone?

	move	*a13(plyr_d_cflgs),a2
	btst	DRN_PASS_B,a2
	jrz	yqbhnopass

	movi	BUT2_M<<8+BUT2_M,a14	;>Make him pass
	jruc	yqbhdocmd

yqbhnopass
	btst	DRN_SHOOT_B,a2
	jrz	yqbhnoshoot
	movi	BUT1_M<<8|BUT1_M|BUT3_M,a14	;>Make him shoot
yqbhdocmd
	move	*a11,a0
	sll	32-4,a0
	srl	32-4,a0
	ori	BUT3_M,a0		;+turbo
yqbhnoturb
	or	a14,a0
	move	a0,*a11
	clr	a0
	move	a0,*a13(plyr_d_cflgs)
	jruc	yqbhx

yqbhnoshoot
yqbhtmdrone

	move	*a13(plyr_d_mode),a14
	subk	2,a14
	jrge	yqbhinmd			;Already in mode?

	movk	2,a1			;Offense with ball
	move	a1,*a13(plyr_d_mode)
	movk	1,a1
	jruc	yqbhsetskc
yqbhinmd

	move	*a13(plyr_d_seekcnt),a1
	jrle	yqbhnotob
yqbhsetskc
	subk	1,a1
	move	a1,*a13(plyr_d_seekcnt)
	jrgt	yqbhnotob

	move	*a13(plyr_ohoopx),*a13(plyr_d_seekx)

	movi	70,a0
	callr	rndrng0
	addi	CZMID-35,a0
	move	a0,*a13(plyr_d_seeky)
yqbhnotob

	move	*a13(plyr_seqflgs),a2
	btst	PASS_B,a2
	jrnz	yqbhkilbuts

	move	*a13(plyr_jmpcnt),a14
	jrnz	yqbhinjmp

	move	*a11,a0
	btst	BUT1_B,a0
	jrnz	yqbhfake			;Shoot button down?


	btst	SHOOT_B,a2
	jrnz	yqbhinjmp

	btst	DUNK_B,a2
	jrnz	yqbhinjmp


	move	*a13(plyr_ohpdist),a4	;A4=Hoop distance

	move	*a13(plyr_dribmode),a14
	jrn	yqbhnodrib
yqbhinspin

	move	*a13(plyr_num),a14	;0CHhk for breakaway
	srl	1,a14
	movk	1,a0
	xor	a0,a14
	sll	6,a14			;*64
	addi	plyrproc_t,a14
	move	*a14+,a2,L
	move	*a14+,a3,L

	move	*a2(plyr_hpdist),a2
	move	*a3(plyr_hpdist),a3

	cmp	a4,a2
	jrlt	yqbhshootrnd		;He's closer?
	cmp	a4,a3
	jrlt	yqbhshootrnd		;He's closer?

	callr	drone_seek


	move	*a13(plyr_num),a1
	XORK	1,a1
	move	@PSTATUS,a14		;Plyr start bits 0-3
	btst	a1,a14
	jrnz	yqbhori			;Teammate is a human?
	move	*a13(plyr_d_skill),a14
	addk	8,a14
	jrle	yqbhnoturb2
yqbhori	ori	BUT3_M,a0		;Push turbo



	move	a0,*a11
yqbhnoturb2

	move	@game_time,a1,L
	srl	8,a1			;Remove tenths
	subk	2,a1
	jrlt	yqbhshoot3			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	yqbhscok
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	yqbhshoot3			;Less than 2 secs?
yqbhscok
	callr	drone_chk3ptr
	jrnz	yqbhshoot3			;Need a 3?

	cmpi	50,a4
	jrlt	yqbhshoot2			;Close?

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	yqbhx			;Teammate is a human?

	cmpi	170,a4
	jrge	yqbhx			;Too far?

	movk	9,a0
	callr	rndrng0
	TEST	a0
	jrnz	yqbhx

	jruc	yqbhshoot2


yqbhnodrib
	move	*a13(plyr_seq),a0
	cmpi	SPIN_MOVE_SEQ,a0
	jreq	yqbhinspin			;Spinning?

	subi	ELBO_SEQ,a0
	jreq	yqbhx			;Elbows?

	subk	ELBO2_SEQ-ELBO_SEQ,a0
	jreq	yqbhx			;Elbows?

	cmpi	240,a4
	jrlt	yqbhshoot2

	callr	drone_pass
	jrnz	yqbhx			;Pass OK?

	movk	01fH,a0
	callr	rnd
	jrnz	yqbhx			;97%?
	jruc	yqbhshoot2


yqbhshootrnd
	move	@game_time,a1,L
	srl	8,a1			;Remove tenths
	subk	2,a1
	jrlt	yqbhshoot3			;Less than 2 secs?
	move	@shotimer+16,a1
	jrnz	yqbhscok2
	move	@shotimer,a1
	subk	2,a1
	jrlt	yqbhshoot3			;Less than 2 secs?
yqbhscok2
	PUSH	a6,a7
	move	*a13(plyr_ohoopx),a6
	movi	CZMID,a7
	calla	seekdirdist_obxz128
	PULL	a6,a7

	move	*a13(plyr_o1dist),a14
	cmpi	80,a14
	jrgt	yqbho1dok			;He's too far?
	cmp	a14,a1
	jrlt	yqbho1dok			;I'm closer?
	move	*a13(plyr_o1dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	yqbho1dsml
	subi	080H,a2
	abs	a2
yqbho1dsml	subk	32,a2
	jrlt	yqbhgoaround		;In front of me?
yqbho1dok
	move	*a13(plyr_o2dist),a14
	cmpi	80,a14
	jrgt	yqbho2dok			;He's too far?
	cmp	a14,a1
	jrlt	yqbho2dok			;I'm closer?
	move	*a13(plyr_o2dir),a2
	sub	a0,a2
	abs	a2
	cmpi	040H,a2
	jrle	yqbho2dsml
	subi	080H,a2
	abs	a2
yqbho2dsml	subk	32,a2
	jrlt	yqbhgoaround		;In front of me?
yqbho2dok
	jruc	yqbhrunath

yqbhgoaround				;>Opponent in my way
	move	*a13(plyr_tmdist),a0
	cmpi	80,a0
	jrlt	yqbhgoa			;Teammate too close?

	callr	drone_pass
	jrnz	yqbhx			;Pass OK?

yqbhgoa
	movi	drnzzcnt,a2
	move	*a2,a0
	subk	1,a0
	jrgt	yqbhzzsame

	move	*a13(plyr_dirtime),a0
	subk	8,a0
	jrle	yqbhzz			;Too little time in dir?

	move	*a13(plyr_attrib_p),a0,L
	move	*a0(PAT_BVEL),a0	;Speed
	cmpi	BV5,a0
	jrle	yqbhzz			;Too slow?


	movk	2,a0
	move	a0,*a13(plyr_tbutn)

	callr	drone_seek
	ori	BUT3_M<<8|BUT3_M,a0	;Turbo
	move	a0,*a11

	jruc	yqbhtryshot

yqbhzz
	movk	5,a0			;New mode
	callr	rndrng0
	ANDK	3,a0
	move	a0,*a2(drnzzmode-drnzzcnt)

	movi	TSEC-10,a0
	callr	rndrng0
	addk	28,a0
yqbhzzsame
	move	a0,*a2

	callr	drone_seek
	sll	3,a0			;*8
	addi	yqbhjbits_t,a0

	move	*a2(drnzzmode-drnzzcnt),a14
	sll	4+3,a14			;*16*8
	add	a14,a0
	movb	*a0,a0
	move	a0,*a11

	move	*a8(OZPOS),a1

	btst	JOYU_B,a0
	jrz	yqbhnju
	cmpi	CZMIN+40,a1
	jrle	yqbhxzmd			;Flip to other circle mode?
yqbhnju
	btst	JOYD_B,a0
	jrz	yqbhnjd
	cmpi	CZMAX-40,a1
	jrlt	yqbhnjd
yqbhxzmd
	move	*a2(drnzzmode-drnzzcnt),a3
	movk	1,a14
	xor	a14,a3
	and	a14,a3
	move	a3,*a2(drnzzmode-drnzzcnt)
yqbhnjd

	cmpi	80,a4
	jrlt	yqbhshoot2			;Close enough for jam?

	jruc	yqbhtryshot


yqbhrunath					;>I have a clr path to hoop!

	callr	drone_seek
	move	*a13(plyr_d_skill),a14
	addk	7,a14
	jrle	yqbhtryshot		;Dumb?
	ori	BUT3_M,a0		;Turbo
	move	a0,*a11


yqbhtryshot
	cmpi	50,a4
	jrlt	yqbhshoot2			;Close enough for jam?

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	yqbhx			;Teammate is a human?

	callr	drone_chk3ptr
	jrnz	yqbhshoot2			;Need a 3?

	cmpi	255,a4
	jrge	yqbhx			;Too far?

	move	*a13(plyr_tmproc_p),a0,L
	move	*a0(plyr_seqflgs),a0
	btst	DUNK_B,a0
	jrnz	yqbhpass			;Alleyoop?


	move	@balltmshotcnt,b0
	subk	TMFIRE_MINCNT-1,b0
	jrne	yqbhnotmheat		;None heating up?

	move	@balltmscored,a1
	srl	5,a1			;0=Tm2, 1=Tm1
	move	*a13(plyr_num),a0
	srl	1,a0
	cmp	a0,a1
	jreq	yqbhnotmheat		;!My tm?

	movi	200,a0
	jruc	yqbhrndsht
yqbhnotmheat

	movi	50,a0
	move	*a13(plyr_d_skill),a14
	addk	7,a14
	jrgt	yqbhrndsht			;Smarter?
	movk	30,a0
yqbhrndsht
	callr	rndrng0
	move	a0,a0
	jrnz	yqbhx


yqbhshoot2
	move	*a13(plyr_tmproc_p),a0,L
	move	*a0(plyr_seqflgs),a0
	btst	DUNK_B,a0
	jrz	yqbhshoot3			;No alleyoop?
yqbhpass
	move	*a11,a0			;>Pass
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11

	jruc	yqbhx

yqbhshoot3
	move	*a11,a0			;>Shoot
	ori	BUT1_M<<8|BUT1_M|BUT3_M,a0
	move	a0,*a11

	jruc	yqbhx



yqbhfake

	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	yqbhx			;Teammate is a human?

	move	*a13(plyr_o1dist),a14
	cmpi	80,a14
	jrlt	yqbhfkc			;He's close?

	move	*a13(plyr_o2dist),a14
	cmpi	80,a14
	jrge	yqbhx			;He's far?
yqbhfkc
	movk	01fH,a0
	callr	rnd
	jrnz	yqbhx

	move	*a9(pld_d_lowsecagr),a14
	move	@game_time,a1,L
	srl	8,a1			;Remove tenths
	cmp	a14,a1
	jrlt	yqbhx			;Less than x secs?
	move	@shotimer+16,a1		;Tens
	jrnz	yqbhfk
	move	@shotimer,a1		;Ones
	cmp	a14,a1
	jrlt	yqbhx			;Less than x secs?
yqbhfk
	jruc	yqbhkilbuts


yqbhinjmp
	move	@PSTATUS,a0		;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrz	yqbhtmdrn			;Teammate is a drone?

	move	*a13(plyr_tmproc_p),a0,L
	move	*a0(PA11),a0,L
	move	*a0,a0			;Get teammates ctrl bits
	btst	BUT1_B,a0
	jrnz	yqbhx			;Holding shoot button?
	jruc	yqbhkilbuts
yqbhtmdrn
	move	*a13(plyr_seqflgs),a0
	btst	BLOCKREB_B,a0
	jrnz	yqbhkilbuts		;Got a rebound?

	btst	DUNK_B,a0
	jrz	yqbhnodnk			;Try alleyoop?

	move	@game_time,a1,L
	srl	8,a1			;Remove tenths
	subk	2,a1
	jrlt	yqbhshoot3			;Less than 2 secs?
	move	@shotimer+16,a1		;Tens
	jrnz	yqbhnodnk
	move	@shotimer,a1		;Ones
	subk	2,a1
	jrlt	yqbhshoot3			;Less than 2 secs?
yqbhnodnk

	move	*a13(plyr_tmproc_p),a0,L
	move	*a0(plyr_seqflgs),a0
	btst	DUNK_B,a0
	jrnz	yqbhpss			;Try alleyoop?


	move	*a13(plyr_num),a2	;0CHhk for close blockers
	srl	1,a2
	XORK	1,a2
	sll	6,a2			;*64
	addi	plyrobj_t,a2

	move	*a2+,a3,L

	move	*a13(plyr_o1dist),a14
	cmpi	70,a14
	jrgt	yqbho1sdok			;He's too far?

	move	*a3(OYPOS),a0
	addk	20,a0
	move	*a8(OYPOS),a1
	cmp	a0,a1
	jrgt	yqbhrndrel			;I'm lower?
yqbho1sdok
	move	*a13(plyr_o2dist),a14
	cmpi	70,a14
	jrgt	yqbhkilbuts		;He's too far? Shoot

	move	*a2+,a3,L

	move	*a3(OYPOS),a0
	addk	20,a0
	move	*a8(OYPOS),a1
	cmp	a0,a1
	jrle	yqbhkilbuts		;I'm higher, so shoot?

yqbhrndrel
	movk	30,a0
	callr	rndrng0
	move	a0,a0
	jrz	yqbhkilbuts		;Cause shoot?

	movk	7,a0
	callr	rnd
	jrnz	yqbhx			;88%?

	move	*a13(plyr_ptsdown),a14
	addk	5,a14
	jrlt	yqbhpss			;Winning by 05H?
	move	*a13(plyr_seqflgs),a14
	btst	DUNK_B,a14
	jrnz	yqbhx			;In a dunk?
	move	@game_time,a1,L
	cmpi	0200H,a1
	jrlt	yqbhx			;Less than 2 secs?
yqbhpss
	callr	drone_pass

	jruc	yqbhx



yqbhkilbuts
	clr	a0			;>Let go of shoot button
	move	a0,*a11

yqbhx
	rets


yqbhjbits_t
	.byte	0,JOYL_M,JOYR_M,0			;90~ clockwise
	.byte	JOYD_M,JOYD_M|JOYL_M,JOYD_M|JOYR_M,0
	.byte	JOYU_M,JOYU_M|JOYL_M,JOYU_M|JOYR_M,0
	.byte	0,0,0,0

	.byte	0,JOYR_M,JOYL_M,0			;90~ cntr clkwise
	.byte	JOYU_M,JOYU_M|JOYR_M,JOYU_M|JOYL_M,0
	.byte	JOYD_M,JOYD_M|JOYR_M,JOYD_M|JOYL_M,0
	.byte	0,0,0,0

	.byte	0,JOYD_M|JOYL_M,JOYU_M|JOYR_M,0		;135~ clkwise
	.byte	JOYD_M|JOYR_M,JOYD_M,JOYR_M,0
	.byte	JOYU_M|JOYL_M,JOYL_M,JOYU_M,0
	.byte	0,0,0,0

	.byte	0,JOYD_M|JOYR_M,JOYU_M|JOYL_M,0		;135~ cntr clkwise
	.byte	JOYU_M|JOYR_M,JOYR_M,JOYU_M,0
	.byte	JOYD_M|JOYL_M,JOYD_M,JOYL_M,0
	.byte	0,0,0,0



 SUBRP	drone_chk3ptr

	cmpi	290,a4
	jrgt	mvsdx0			;Too far?

	move	*a13(plyr_num),a1
	move	@PSTATUS,a0		;Plyr start bits 0-3
	movk	1,a14
	xor	a14,a1
	btst	a1,a0
	jrnz	mvsdx0			;Teammate is a human?

	xor	a14,a1
	move	@plyr_onfire,a0
	btst	a1,a0
	jrnz	mvsdx1			;I'm on fire?

	cmpi	230,a4
	jrlt	mvsdx0			;Too close?

	XORK	2,a1
	btst	a1,a0
	jrnz	mvsdx0			;Opp 1 on fire?
	XORK	1,a1
	btst	a1,a0
	jrnz	mvsdx0			;Opp 2 on fire?


	movk	6,a1
	move	@game_time,a0,L
	cmpi	01010000H,a0
	jrgt	mvsdhvtime			;Enough time?
	movk	3,a1
mvsdhvtime
	move	*a13(plyr_ptsdown),a14
	cmp	a1,a14
	jrlt	mvsdx0

	cmpi	040000H,a0
	jrlt	mvsdx1			;Less than 40 secs?

	move	*a13(plyr_o1dist),a14
	cmpi	70,a14
	jrlt	mvsdrndsht			;He's close?
	move	*a13(plyr_o2dist),a14
	cmpi	70,a14
	jrge	mvsdx1			;He's far?
mvsdrndsht
	movk	8,a0
	callr	rndrng0
	move	a0,a0
	jrnz	mvsdx0

mvsdx1
	addk	1,a0
	rets
mvsdx0
	clr	a0
	rets



 SUBRP	drone_offwoball

	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	callr	drone_getcurskillo
	move	a0,a5				;A5=Skill offset


	movk	1,a4				;A4=Fire flag (+=Me, -=Tm)
	move	*a13(plyr_num),a0
	move	@plyr_onfire,a1
	btst	a0,a1
	jrnz	fhqamefr				;I'm on fire?

	subk	2,a4				;=-1

	XORK	1,a0
	btst	a0,a1
	jrnz	fhqamefr				;Tm on fire?
	clr	a4
fhqamefr



	move	*a13(plyr_d_mode),a14
	subk	1,a14
	jreq	fhqainmd				;Already in mode?

	movk	1,a0				;Offense wo ball
	move	a0,*a13(plyr_d_mode)

	move	a5,a0
	addi	fhqamdsk_t,a0
	move	*a0,a0
	callr	rndrng0
	addk	1,a0
	move	a0,*a13(plyr_d_seekcnt)
fhqainmd


	move	*a13(plyr_dir),a3

	move	*a13(plyr_o1dist),a14
	subi	50,a14
	jrgt	fhqao1far				;Too far?
	move	*a13(plyr_o1dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	fhqao1dsml
	subi	080H,a2
	abs	a2
fhqao1dsml	subk	16,a2
	jrlt	fhqapusho				;In front of me?
fhqao1far
	move	*a13(plyr_o2dist),a14
	subi	50,a14
	jrgt	fhqanopush				;Too far?
	move	*a13(plyr_o2dir),a2
	sub	a3,a2
	abs	a2
	cmpi	040H,a2
	jrle	fhqao2dsml
	subi	080H,a2
	abs	a2
fhqao2dsml	subk	16,a2
	jrge	fhqanopush				;!In front?
fhqapusho
	movi	99,a0
	callr	rndrng0

	move	a5,a14
	addi	fhqap_t,a14
	move	*a14,a1

	TEST	a4
	jrle	fhqapshnf				;No fire?
	addk	30,a1				;Push alot more!
fhqapshnf
	move	@balltmshotcnt,b0
	subk	TMFIRE_MINCNT-1,b0
	jrlt	fhqanoth				;None heating up?
	addk	20,a1				;Push more!
fhqanoth
	cmp	a1,a0
	jrge	fhqanewseek			;Skip push?

	.if	NOPUSHSTEAL
	jruc	fhqax
	.endif

	move	*a11,a0				;Push
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11

	jruc	fhqax

fhqanopush

	move	*a13(plyr_ohpdist),a3
	cmpi	65,a3
	jrle	fhqanoalyo				;Too close?
	cmpi	180,a3
	jrgt	fhqanoalyo				;Too far?

	TEST	a4
	jrnz	fhqanoalyo				;We on fire?

	move	a5,a14
	addi	fhqaaly_t,a14
	move	*a14,a2

	move	*a13(plyr_tmproc_p),a1,L
	move	*a1(plyr_seqflgs),a14
	btst	DUNK_B,a14
	jrz	fhqanorm				;Tm not dunking?

	move	*a1(plyr_slam_ticks),a14
	move	*a1(plyr_jmpcnt),a1
	sub	a1,a14
	subk	20,a14
	jrle	fhqanoalyo				;Not enough time?

fhqanorm
	move	*a13(plyr_d_seeky),a0
	cmpi	CZMID+1,a0
	jrne	fhqarndaly				;!in allyo seek?

	callr	drone_seek
	ori	BUT1_M<<8|BUT1_M|BUT3_M,a0	;Turbo shoot
	move	a0,*a11


	jruc	fhqax

fhqarndaly
	move	@balltmshotcnt,b0
	subk	TMFIRE_MINCNT-1,b0
	jrlt	fhqanotmheat			;None heating up?


	sll	2,a2				;% * 2
fhqanotmheat

	movi	99,a0
	callr	rndrng0
	cmp	a2,a0
	jrge	fhqanoalyo

	cmpi	80,a3
	jrle	fhqanoalyo				;Too close?

	move	*a13(plyr_PDATA_p),a1,L
	move	*a1(ply_turbo),a14
	subk	3,a14
	jrle	fhqanoalyo				;No turbo?

	move	*a13(plyr_ohoopx),a0
	movi	CZMID+1,a1
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)

	callr	drone_seekxy

	jruc	fhqax

fhqanoalyo



	move	*a13(plyr_d_seeky),a0
	cmpi	CZMID+1,a0
	jreq	fhqanewseek			;Failed allyo?

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	fhqaseek

fhqanewseek

	movk	16-1,a0

	TEST	a4
	jrle	fhqano3				;I'm not on fire?
	movk	7-1,a0
fhqano3
	.if	ONLY3
	movk	7-1,a0
	.endif

	callr	rndrng0
	sll	5,a0				;*32
	addi	fhqaseek_t,a0

	move	*a0+,a1

	move	*a13(plyr_ohoopx),a14
	cmpi	WRLDMID,a14
	jrlt	fhqalft
	neg	a1
fhqalft
	add	a1,a14
	move	a14,*a13(plyr_d_seekx)

	move	*a0+,a1
	move	a1,*a13(plyr_d_seeky)

	movi	TSEC*3/2,a0
	callr	rndrng0
	addk	TSEC/2,a0

fhqaseek
	move	a0,*a13(plyr_d_seekcnt)
fhqacontsk
	callr	drone_seek
	jrnz	fhqanotthere

	movk	01fH,a0				;3%
	callr	rnd
	jrnz	fhqax

	clr	a0
	move	a0,*a13(plyr_d_seekcnt)

fhqanotthere
	TEST	a4
	jrle	fhqanotur				;I'm not on fire?

	move	*a11,a0
	ori	BUT3_M,a0			;+turbo
	move	a0,*a11
fhqanotur


fhqax
	rets


fhqamdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	40,40,40,35,30		;10-6
	.word	25,22,20,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

fhqap_t					;% to push
	.word	1,1,1,1,1
	.word	2,2,2,2,3
	.word	3,3,4,4,5
	.word	5
	.word	5,6,8,10,13
	.word	15,17,18,20,20
	.word	25,30,35,40,50

	.asg	10,N
fhqaaly_t					;% to jump at backboard
	.word	1,2,3,4,5
	.word	N+05,N+10,N+15,N+15,N+20
	.word	N+20,N+20,N+20,N+22,N+25
	.word	N+25
	.word	N+25,N+26,N+28,N+30,N+35
	.word	N+40,N+45,N+50,N+55,N+60
	.word	N+65,N+70,N+75,N+90,N+99


	.asg	CZMID,Z
fhqaseek_t
	.word	0,Z-150, 80,Z-150, 200,Z-100	;3ptrs
	.word	255,Z
	.word	200,Z+115, 80,Z+190, 0,Z+190

	.word	0,Z-100, 50,Z-90, 100,Z-80	;2ptrs
	.word	150,Z
	.word	100,Z+100, 50,Z+110, 0,Z+120

	.word	30,Z-40, 30,Z+40



 SUBRP	drone_pass

	move	@PSTATUS,a0			;Plyr start bits 0-3
	move	*a13(plyr_num),a1
	XORK	1,a1
	btst	a1,a0
	jrnz	ulecx				;Teammate is a human?


	move	*a13(plyr_tmdist),a0
	addk	30,a0

	move	*a13(plyr_o1dist),a1		;0CHhk if o1 in my way
	cmp	a1,a0
	jrlt	uleco1ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o1dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	ulecdsml
	subi	128,a1
	abs	a1
ulecdsml
	subk	16,a1
	jrlt	ulecinway
uleco1ok

	move	*a13(plyr_o2dist),a1		;0CHhk if o2 in my way
	cmp	a1,a0
	jrlt	uleco2ok

	move	*a13(plyr_tmdir),a14
	move	*a13(plyr_o2dir),a1
	sub	a14,a1
	abs	a1
	cmpi	64,a1
	jrle	ulecdsml2
	subi	128,a1
	abs	a1
ulecdsml2
	subk	16,a1
	jrlt	ulecinway
uleco2ok

	move	@ballnumscored,b0
	subk	ONFIRE_MINCNT-1,b0
	jrlt	ulecnoheat				;None heating up?

	move	@ballpnumscored,a1
	move	*a13(plyr_num),a0
	cmp	a0,a1
	jrne	ulecnoheat				;!Me?

	move	*a13(plyr_ohpdist),a0
	cmpi	350,a0
	jrlt	ulecx				;Too close? Don't pass
ulecnoheat

uleciwpass
	move	*a13(plyr_tmproc_p),a1,L
ulectmclos
	move	*a1(plyr_seqflgs),a0
	btst	DUNK_B,a0
	jrnz	ulecpass				;Tm dunking?

	move	*a1(plyr_seq),a0
	subk	RUNDRIBTURB_SEQ,a0
	jrhi	ulecx				;Tm is doing something?
ulecpass
	move	*a11,a0				;>Pass
	ori	BUT2_M<<8|BUT2_M|BUT3_M,a0
	move	a0,*a11

	rets


ulecinway
	move	@ballnumscored,b0
	subk	ONFIRE_MINCNT-1,b0
	jrlt	ulecnoheatiw			;None heating up?

	move	@ballpnumscored,a1
	move	*a13(plyr_num),a0
	XORK	1,a0
	cmp	a0,a1
	jrne	ulecnoheatiw			;!Tm?

	move	*a13(plyr_tmproc_p),a1,L
	move	*a1(plyr_ohpdist),a0
	cmpi	300,a0
	jrlt	uleciwpass				;He's Close? Risk pass

ulecnoheatiw
	move	*a13(plyr_ohpdist),a1
	cmpi	250,a1
	jrlt	ulecx				;I'm close to hoop?

	move	*a13(plyr_tmproc_p),a1,L
	move	*a1(plyr_ohpdist),a0
	cmpi	240,a0
	jrlt	ulectmclos				;Teammate is close to hoop?

ulecx
	clr	a0
	rets




 SUBRP	drone_defense

	PUSH	a6,a7,a10

	clr	a0
	move	a0,*a13(plyr_d_cflgs)


	move	@ballpnum,a5
	sll	5,a5
	addi	plyrproc_t,a5
	move	*a5,a5,L		;A5=*Proc of opponent with ball


	callr	drone_getcurskillo
	move	a0,a7			;A7=Ptsdn+skill for indexing (*16)


	movk	1,a6			;A6=Fire flag (+=Me, -=Tm)
	move	*a13(plyr_num),a0
	move	@plyr_onfire,a1
	btst	a0,a1
	jrnz	hpxkmefr			;I'm on fire?

	subk	2,a6			;=-1

	XORK	1,a0
	btst	a0,a1
	jrnz	hpxkmefr			;Tm on fire?
	clr	a6
hpxkmefr



	move	*a13(plyr_d_mode),a14
	jrn	hpxkinmd			;Already in mode?

	not	a14
	move	a14,*a13(plyr_d_mode)	;Neg

	move	a7,a14
	addi	hpxkmdsk_t,a14
	move	*a14,a0
	move	a0,a2
	srl	2,a2			;/4

	callr	rndrng0
	addk	1,a0
	add	a2,a0
	move	a0,*a13(plyr_d_seekcnt)

	clr	a0
	move	a0,*a9(pld_d_nastycnt)

	movk	30,a0
	callr	rndrng0

	addi	190-20,a0
	move	a0,*a9(pld_d_grddist)
hpxkinmd


	move	*a9(pld_d_lowsecagr),a0

	movk	2,a10
	move	@game_time,a14,L
	srl	8,a14			;Remove tenths
	cmp	a0,a14
	jrlt	hpxknasty			;Less than x secs?

	move	@gmqrtr,a2
	subk	3,a2
	jrlt	hpxkchkst
	move	*a13(plyr_ptsdown),a1
	addk	3,a1
	jrle	hpxkchkst			;Winning by 3 or more?
	srl	8,a14			;Remove ones
	sll	1,a14			;*2
	cmp	a0,a14
	jrlt	hpxknasty			;Less than x0 secs?
hpxkchkst
	move	@shotimer+16,a14	;Tens
	jrnz	hpxkscok
	move	@shotimer,a14		;Ones
	srl	1,a0			;/2
	cmp	a0,a14
	jrlt	hpxknasty			;Less than x secs?
hpxkscok
	move	*a9(pld_d_nastycnt),a10
	jrgt	hpxknaston

	movk	10,a10
	movi	0ffh,a0
	callr	rnd
	jrz	hpxknasty

	clr	a10

	movi	999,a0
	callr	rndrng0

	move	a7,a14
	addi	hpxknast_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	hpxknonast			;No nasty?

	movi	TSEC-20,a0
	callr	rndrng0
	addk	20,a0
	move	a0,a10
hpxknaston
	subk	1,a10
hpxknasty
	move	a10,*a9(pld_d_nastycnt)

	cmpi	10,a10
	jreq	hpxknewsk
hpxknonast


	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	hpxkseek
hpxknewsk
	move	*a13(plyr_num),a2
	XORK	2,a2
	move	a2,a4
	sll	5,a4			;*32
	addi	plyrproc_t,a4
	move	*a4,a4,L
	cmp	a5,a4
	jreq	hpxkguard			;I'm on guy with ball?

	move	*a5(plyr_ohpdist),a0
	cmpi	300,a0
	jrgt	hpxkguard			;Too far to worry about?


	move	*a13(plyr_tmproc_p),a3,L
	move	*a3(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	hpxktmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	hpxkgbc			;Teammate staggered?
hpxktmok
	move	*a3(plyr_o1dist),a14
	move	*a3(plyr_o1dir),a1
	btst	0,a2
	jrnz	hpxkp1
	move	*a3(plyr_o2dist),a14
	move	*a3(plyr_o2dir),a1
hpxkp1
	move	*a3(plyr_hpdir),a0	;Find dir difference
	sub	a1,a0
	abs	a0
	cmpi	040H,a0
	jrle	hpxkdsml
	subi	080H,a0
	abs	a0
hpxkdsml	subk	28,a0
	jrle	hpxkgbc			;TM not between op and hoop?

	cmpi	160,a14
	jrle	hpxkguard			;TM guarding?

hpxkgbc
	move	a5,a4			;Guard ball carrier
hpxkguard


	move	*a4(PA8),a2,L		;*Obj

	move	*a2(OXPOS),a0
	move	*a2(OXANI+16),a14
	add	a14,a0			;X
	move	*a2(OZPOS),a1		;Z

	move	*a2(OXVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a0
	move	*a2(OZVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a1


	TEST	a10
	jrgt	hpxksetseek		;Nasty on?


	move	*a5(plyr_seqflgs),a14
	btst	SHOOT_B,a14
	jrz	hpxknosh			;!Starting a shot?

	TEST	a6
	jrgt	hpxknosh			;I'm on fire?

	move	*a13(plyr_balldist),a14
	cmpi	70,a14
	jrle	hpxksetseek		;In his face?

hpxknosh
	move	*a13(plyr_myhoopx),a2	;Stay near oplyr between my basket
	movi	CZMID,a3

	sub	a2,a0
	sub	a3,a1

	move	*a9(pld_d_grddist),a14

	TEST	a6
	jrle	hpxknofire			;I'm not on fire?

	movi	77,a14			;30%

	move	a4,b0
	move	*b0(plyr_ohpdist),b0
	cmpi	400,b0
	jrge	hpxknofire			;Opp far from my hoop?
	movi	154,a14			;60%
hpxknofire
	mpys	a14,a1
	sra	8,a1			;/256
	add	a1,a3

	move	a0,a1
	mpys	a14,a1
	sra	8,a1			;/256
	add	a1,a2

	move	a2,a0
	move	a3,a1

hpxksetseek
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)

	movk	25,a0
	move	*a4(plyr_ohpdist),a1
	cmpi	320,a1
	jrge	hpxkseek			;Opp far from my hoop?

	move	a7,a14
	addi	hpxkskt_t,a14
	move	*a14,a0
	srl	1,a0			;/2
	move	a0,a2
	callr	rndrng0
	add	a2,a0

hpxkseek
	move	a0,*a13(plyr_d_seekcnt)

	callr	drone_seek
	move	a0,a2


	move	*a13(plyr_num),a14	;>Get opponents proc
	addk	2,a14
	sll	32-2,a14
	srl	32-2-5,a14		;*32
	addi	plyrproc_t,a14
	move	*a14,a0,L

	move	*a0(plyr_ohpdist),a0
	subk	10,a0
	cmpi	300,a0
	jrgt	hpxkonclose		;!close?
	subi	60,a0			;Turbo earlier
hpxkonclose
	move	*a13(plyr_hpdist),a1
	cmp	a0,a1
	jrlt	hpxkicloser

	TEST	a2
	jrz	hpxkicloser		;I'm not moving?
	addi	BUT3_M,a2		;Turbo
hpxkicloser


	move	*a13(plyr_balldist),a1
	cmpi	35,a1
	jrgt	hpxkpsrnd			;!In his face?

	move	@PCNT,a0
	sll	32-1,a0
	jrnz	hpxkskipsp			;Skip 50%?

	move	a10,a10
	jrgt	hpxkps			;Nasty on?

hpxkpsrnd
	move	a7,a14
	addi	hpxkdist_t,a14
	move	*a14,a0
	cmp	a0,a1
	jrgt	hpxkskipsp			;Ball too far?

	movi	999,a0
	callr	rndrng0

	move	a7,a14
	addi	hpxkps_t,a14
	move	*a14,a1
	cmp	a1,a0
	jrge	hpxkskipsp			;Skip push?
hpxkps
	movk	1,a0
	callr	rnd
	jrnz	hpxkpush			;50%?


	move	*a5(plyr_jmpcnt),a1
	jrnz	hpxkpush			;Plyr with ball is in air?

	sll	32-4,a2
	srl	32-4,a2
	addk	BUT2_M,a2		;Steal
	jruc	hpxkx

hpxkpush
	move	*a5(PA8),a0,L
	move	*a0(OYPOS),a1
	move	*a5(plyr_aniy),a14
	add	a14,a1			;His feet Y position
	move	*a8(OYPOS),a0
	sub	a1,a0
	addk	10,a0
	jrgt	hpxkskipsp			;Feet above my shoulders?

	ori	BUT2_M<<8|BUT2_M|BUT3_M,a2
	jruc	hpxkx

hpxkskipsp


	move	*a13(plyr_balldist),a14
	cmpi	65,a14
	jrge	hpxknoblk

	move	*a5(plyr_seqflgs),a4


	btst	DUNK_B,a4
	jrz	hpxknodnk

	cmpi	35,a14
	jrgt	hpxknoblk

	move	*a13(plyr_tmproc_p),a3,L

	move	*a3(plyr_balldist),a14
	cmpi	45,a14
	jrgt	hpxktryblk			;Teammate far?

	move	*a3(plyr_jmpcnt),a1
	jrz	hpxktryblk			;Tm on gnd?

	move	@PCNT,a0
	sll	32-3,a0
	jrnz	hpxknoblk			;Skip 88%?

	jruc	hpxktryblk

hpxknodnk
	btst	SHOOT_B,a4
	jrz	hpxknoblk			;!Starting a shot?

	move	*a5(plyr_jmpcnt),a1
	jrnz	hpxktryblk			;Plyr with ball is in air?

	movk	11,a0
	callr	rndrng0
	TEST	a0
	jrnz	hpxknoblk			;92% ignore?
	jruc	hpxkblk

hpxktryblk
	movi	99,a0
	callr	rndrng0
	move	a7,a14
	addi	hpxkblk_t,a14
	move	*a14,a1

	btst	DUNK_B,a4
	jrz	hpxknd
	sra	1,a1			;Dunk % /2
hpxknd
	TEST	a10
	jrle	hpxknstoff			;Nasty off?
	sll	1,a1			;%*2
hpxknstoff
	cmp	a1,a0
	jrge	hpxknoblk

hpxkblk
	sll	32-4,a2
	srl	32-4,a2
	addi	BUT1_M<<8|BUT1_M|BUT3_M,a2 ;Block
hpxknoblk

hpxkx
	.if	NOPUSHSTEAL
	andi	0dfh,a2
	.endif

	move	a2,*a11


	PULL	a6,a7,a10
	rets


hpxkmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,45,45,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

	.asg	0,N
hpxknast_t					;% to become nasty
	.word	0,0,0,0,0
	.word	N+1,N+1,N+2,N+2,N+2
	.word	N+3,N+3,N+3,N+4,N+5
	.word	N+5
	.word	N+6,N+7,N+8,N+9,N+10
	.word	N+12,N+13,N+14,N+15,N+16
	.word	N+18,N+20,N+24,N+28,N+30

	.asg	25,N
hpxkskt_t					;Max seek time (75% avrg)
	.word	N+55,N+55,N+55,N+55,N+52
	.word	N+48,N+44,N+40,N+36,N+33
	.word	N+30,N+29,N+28,N+27,N+26
	.word	N+25			;Even score
	.word	N+24,N+23,N+21,N+19,N+17
	.word	N+14,N+11,N+08,N+06,N+03
	.word	N+01,N-01,N-03,N-05,N-10
hpxkdist_t					;Max dist to try push/steal
	.word	110,100,100,100,100
	.word	90,90,80,80,80
	.word	80,70,70,60,60
	.word	60
	.word	50,50,50,50,50
	.word	50,50,50,50,50
	.word	50,50,50,50,50
hpxkps_t					;% to push/steal
	.word	1,2,2,2,2
	.word	3,3,3,3,3
	.word	4,4,4,5,5
	.word	6
	.word	6,6,8,10,13
	.word	15,17,18,20,30
	.word	40,60,80,150,250
hpxkblk_t					;% to block
	.word	1,1,2,3,3
	.word	3,3,4,4,5
	.word	6,7,8,10,12
	.word	14
	.word	16,18,20,25,30
	.word	35,40,45,50,50
	.word	50,50,50,50,50



 SUBR	drone_setuptob

	PUSH	a2,a3,a4,a13

	movk	4,a4
	movi	plyrproc_t,a3

dfnllp
	move	*a3+,a13,L
	move	*a13(PA9),a2,L

	movk	1,a0
	move	*a13(plyr_ownball),a14
	jrz	dfnldef			;Defense?
					;>Setup offense
	jrn	dfnlwob
	movk	2,a0
dfnlwob
	move	a0,*a13(plyr_d_mode)

	movi	TSEC-10,a0
	callr	rndrng0
	addk	5,a0
	move	a0,*a13(plyr_d_seekcnt)

	movk	9-1,a0
	callr	rndrng0
	sll	5,a0			;*32
	addi	dfnlseek_t,a0

	move	*a0+,a1
	move	*a13(plyr_num),a14
	subk	2,a14
	jrlt	dfnllft
	neg	a1
dfnllft
	addi	WRLDMID,a1
	move	a1,*a13(plyr_d_seekx)

	move	*a0+,a1
	move	a1,*a13(plyr_d_seeky)

	jruc	dfnlnxt


dfnldef					;>Setup defense
	movi	-1,a14
	move	a14,*a13(plyr_d_mode)	;Defense

	clr	a0
	move	a0,*a2(pld_d_nastycnt)

	movi	190,a0
	move	a0,*a2(pld_d_grddist)

	movk	4,a0
	callr	rndrng0
	addk	5,a0
	move	a0,*a2(pld_d_lowsecagr)	;5-9

	callr	drone_getcurskillo

	addi	dfnlmdsk_t,a0
	move	*a0,a0
	callr	rndrng0
	addk	1,a0
	move	a0,*a13(plyr_d_seekcnt)

dfnlnxt
	dsj	a4,dfnllp

	PULL	a2,a3,a4,a13
	rets


	.asg	CZMID,Z
dfnlseek_t
	.word	-260,Z-160, -200,Z-150,	-100,Z-130, -50,Z-100
	.word	0,Z
	.word	-260,Z+160, -200,Z+150,	-100,Z+130, -50,Z+100


dfnlmdsk_t					;Mode switch max seek time
	.word	30,30,30,25,25		;Up 15-11
	.word	25,25,20,20,20		;10-6
	.word	20,18,16,14,12		;5-1
	.word	10			;Even score
	.word	8,7,6,5,4		;Dn 1-5
	.word	4,3,3,2,2		;6-10
	.word	2,1,1,1,1		;11-15



 SUBRP	drone_chaseball

	move	*a13(plyr_rcvpass),a14
	jrgt	drone_offwoball		;I'm rcving ball?

	move	*a13(plyr_tmproc_p),a4,L
	move	*a4(plyr_rcvpass),a14
	jrgt	drone_offwoball		;Teammate rcving ball?



	clr	a0
	move	a0,*a13(plyr_d_cflgs)

	move	*a13(plyr_d_mode),a14
	jrz	aswminmd			;Already in mode?

	move	a0,*a13(plyr_d_mode)

	callr	drone_getcurskillo
	addi	aswmmdsk_t,a0
	move	*a0,a0

	callr	rndrng0
	addk	2,a0
	move	a0,*a13(plyr_d_seekcnt)

aswminmd

	move	*a13(plyr_d_seekcnt),a0
	subk	1,a0
	jrgt	aswmseek

	move	@ballobj_p,a5,L


	move	*a13(plyr_num),a14	;0CHhk for pass
	srl	1,a14
	movk	1,a0
	xor	a0,a14
	sll	6,a14			;*64
	addi	plyrproc_t,a14

	move	*a14+,a1,L
	move	*a1(plyr_rcvpass),a0
	jrgt	aswmchaseb			;Opp rcving ball?

	move	*a14+,a1,L
	move	*a1(plyr_rcvpass),a0
	jrgt	aswmchaseb			;Opp rcving ball?


	move	*a4(plyr_seq),a0
	subi	STAGGER_SEQ,a0
	jrls	aswmtmok
	subk	FLYBACKWB2_SEQ-STAGGER_SEQ,a0
	jrls	aswmchaseb			;Teammate staggered?
aswmtmok
	move	*a4(plyr_jmpcnt),a14
	jrnz	aswmchaseb			;Tm in air?

	movi	CZMID,a1

	move	*a13(plyr_num),a0
	move	@plyr_onfire,a2
	btst	a0,a2
	jrz	aswmnof			;I'm not on fire?

	XORK	1,a0
	btst	a0,a2
	jrz	aswmskf			;Tm not on fire?

	move	*a13(plyr_balldist),a0
	move	*a4(plyr_balldist),a14
	cmp	a14,a0
	jrle	aswmchaseb			;I'm closer?
aswmskf
	move	*a13(plyr_myhoopx),a0	;Go in front of my hoop for goaltend
	move	a0,a14
	subi	WRLDMID,a14
	sra	4,a14			;/16
	sub	a14,a0
	jruc	aswmsetxz

aswmnof
	XORK	1,a0
	btst	a0,a2
	jrnz	aswmchaseb			;Tm on fire? I get ball

	move	*a13(plyr_balldist),a0
	move	*a4(plyr_balldist),a14
	cmp	a14,a0
	jrle	aswmchaseb			;I'm closer?

	move	*a5(OYVEL+16),a0
	jrlt	aswmchaseb			;Going up?

	move	*a13(plyr_ohoopx),a0	;Go for opponents top of 3 pt line
	subi	WRLDMID,a0
	sra	2,a0			;/4
	addi	WRLDMID,a0
	jruc	aswmsetxz

aswmchaseb
	move	*a5(OXPOS),a0
	move	*a5(OXANI+16),a14
	add	a14,a0
	move	*a5(OXVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a0

	move	*a5(OZPOS),a1
	move	*a5(OZVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a1
aswmsetxz
	move	a0,*a13(plyr_d_seekx)
	move	a1,*a13(plyr_d_seeky)


	callr	drone_getcurskillo
	addi	aswmskt_t,a0
	move	*a0,a0

	callr	rndrng0
	addk	5,a0

aswmseek
	move	a0,*a13(plyr_d_seekcnt)

	callr	drone_seek
	jrz	aswmsk0
	ori	BUT3_M,a0		;Turbo
	move	a0,*a11
aswmsk0



	move	*a13(plyr_balldist),a0
	cmpi	100,a0
	jrgt	aswmnojmp

	move	@ballobj_p,a5,L


	move	*a5(OXPOS),a0		;0CaHlc distance (long+short/2.667)
	move	*a5(OXANI+16),a14
	add	a14,a0
	move	*a5(OXVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a0

	move	*a5(OZPOS),a1
	move	*a5(OZVEL),a14,L
	sra	16-4,a14		;16 ticks from now
	add	a14,a1

	move	*a8(OXPOS),a2
	move	*a8(OXANI+16),a14
	add	a14,a2
	move	*a8(OZPOS),a3

	sub	a0,a2
	abs	a2
	sub	a1,a3
	abs	a3

	cmp	a2,a3
	jrge	aswma3bg
	SWAP	a2,a3
aswma3bg
	srl	1,a2			;Shorter/2
	add	a2,a3
	srl	2,a2			;Shorter/8
	sub	a2,a3			;A3=Dist in 16 ticks


	cmpi	60,a3
	jrgt	aswmnojmp


	move	@balltmshotcnt,b0
	subk	TMFIRE_MINCNT-1,b0
	jrne	aswmnotmheat		;None heating up?

	move	@gmqrtr,b0
	subk	3,b0
	jrlt	aswmtmhqok			;Q123?

	move	@game_time,a14,L
	srl	8+8,a14			;Remove one & tenths
	subk	6,a14
	jrlt	aswmnotmheat		;Less than 60 secs?
aswmtmhqok
	move	@balltmscored,a1
	srl	5,a1			;0=Tm2, 1=Tm1
	move	*a13(plyr_num),a0
	srl	1,a0
	cmp	a0,a1
	jrne	aswmifire			;My tm?
aswmnotmheat

	move	*a13(plyr_num),a1
	move	@plyr_onfire,a0
	btst	a1,a0
	jrnz	aswmifire			;I'm on fire?

	XORK	1,a1
	btst	a1,a0
	jrz	aswmnofire			;Tm not on fire?
aswmifire
	move	*a13(plyr_hpdist),a14
	cmpi	150,a14
	jrge	aswmnofire			;Too far for goaltend?

	move	*a8(OYPOS),a0
	move	*a5(OYPOS),a1
	sub	a1,a0
	subk	20,a0
	jrle	aswmnofire			;Ball too low?

	subi	160-20,a0
	jrgt	aswmnojmp			;Ball too high?

	move	*a5(OYVEL+16),a0
	jrgt	aswmj			;Going down? Goaltend!
	jruc	aswmnojmp

aswmnofire
	move	*a8(OYPOS),a0
	move	*a5(OYPOS),a1
	sub	a1,a0
	subk	10,a0
	jrgt	aswmnojmp			;Ball too high?

	addk	32,a1
	jrge	aswmnojmp			;Ball close to gnd?

	movk	7,a0
	callr	rnd
	jrnz	aswmnojmp			;87%?

	move	*a11,a2
	sll	32-4,a2
	srl	32-4,a2
	addk	BUT2_M,a2		;Steal
	move	a2,*a11
	jruc	aswmnojmp

aswmj
	move	*a11,a0			;Jmp
	ori	BUT1_M<<8|BUT1_M|BUT3_M,a0
	move	a0,*a11
aswmnojmp

	rets


aswmmdsk_t					;Mode switch max seek time
	.word	50,50,50,50,50		;Up 15-11
	.word	50,50,40,40,40		;10-6
	.word	33,25,22,18,16		;5-1
	.word	14			;Even score
	.word	10,8,6,4,4		;Dn 1-5
	.word	4,3,3,3,2		;6-10
	.word	2,2,2,2,1		;11-15

aswmskt_t					;Max seek time
	.word	50,50,45,45,45
	.word	40,40,30,30,22
	.word	20,17,16,15,15
	.word	15
	.word	15,14,14,13,13
	.word	12,12,12,12,12
	.word	11,11,11,10,10



 SUBRP	drone_seek

	move	*a13(plyr_d_seekx),a0
	move	*a13(plyr_d_seeky),a1


 SUBRP	drone_seekxy

	move	a2,b0

	move	*a8(OXPOS),a2
	move	*a8(OXANI+16),a14
	add	a14,a2
	sub	a0,a2

	clr	a0

	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	ciogonx
	move	a14,a14
	jrlt	ciognolft
	subk	4,a0			;Left

ciognolft	addk	8,a0			;Rgt
ciogonx
	move	*a8(OZPOS),a2

	sub	a1,a2
	move	a2,a14
	abs	a2
	subk	10,a2
	jrle	ciogonz
	move	a14,a14
	jrlt	ciognoup
	subk	1,a0			;Up

ciognoup	addk	2,a0			;Dn
ciogonz
	move	a0,*a11

	move	b0,a2
	move	a0,a0
	rets



 SUBRP	drone_getcurskillo

	move	*a13(plyr_ptsdown),a0
	move	*a13(plyr_d_skill),a14
	add	a14,a0


	move	*a13(plyr_num),a1
	move	@dronesmrt,a14
	btst	a1,a14
	jrz	xkrknosmrt			;Normal?
	addk	5,a0
xkrknosmrt


	move	@ballnumscored,a1
	subk	ONFIRE_MINCNT-1,a1
	jrlt	xkrknoheat			;None heating up?

	addk	2,a0			;Get tougher

	move	@ballpnumscored,a1
	move	*a13(plyr_num),a14
	cmp	a1,a14
	jrne	xkrknoheat			;Not me?

	addk	6,a0			;Get tougher
xkrknoheat

	subk	15,a0
	jrle	xkrkmxdnok
	clr	a0
xkrkmxdnok
	addk	15+15,a0
	jrge	xkrkdnok
	clr	a0
xkrkdnok
	sll	4,a0			;Ptsdn+skill for indexing (*16)

	rets




 SUBR	drone_adjskill

	PUSH	a2,a3,a4,a5,a6

	move	a0,a5
	subk	2,a5
	abs	a5
	move	@gmqrtr,a1
	cmpi	3,a1
	jrls	iqsxqok
	movk	3,a1			;Overtime
iqsxqok
	movk	3,a0
	mpyu	a0,a1
	add	a1,a5			;A5=Quarter+minute index (0-11)


	movk	ADJDIFF,a0		;Get difficulty level
	calla	GET_ADJ			;1-5
	subk	4,a0			;-3 to 1
	move	a0,a6
	sll	1,a0			;*2
	add	a0,a6			;A6=Difficulty adj (-9,-6,-3,0,3)


	movi	plyrproc_t,a4
	movk	4,b0
iqsxlp
	move	*a4+,a3,L

	move	*a3(plyr_d_skill),a2

	move	*a3(plyr_ptsdown),a14
	subk	15,a14
	jrle	iqsxmxdnok
	clr	a14
iqsxmxdnok
	addk	15+15,a14		;0-30
	jrge	iqsxdnok
	clr	a14
iqsxdnok
	sll	4,a14
	addi	iqsxadj_t,a14
	move	*a14,a14
	add	a14,a2			;Modify skill

	move	a5,a14			;0CHhk minute minimum
	sll	3,a14
	addi	iqsxmin_t,a14
	movb	*a14,a14
	add	a6,a14
	cmp	a14,a2
	jrge	iqsxminok			;Min OK?
	move	a14,a2
iqsxminok

	move	*a3(plyr_num),a1
	XORK	1,a1
	move	@PSTATUS,a14
	btst	a1,a14
	jrnz	iqsxdone			;Teammate is human?


	move	@team1,a1		;0CHhk team minimum
	cmpi	3,b0
	jrge	iqsxt1
	move	@team2,a1
iqsxt1
	cmpi	29,a1
	jrlo	iqsxtnumok
	movk	1,a1
iqsxtnumok
	movk	12,a0
	mpyu	a0,a1

	add	a5,a1
	sll	3,a1			;*8
	addi	iqsxtdmin_t,a1
	movb	*a1,a14
	add	a6,a14
	cmp	a14,a2
	jrge	iqsxtminok
	move	a14,a2
iqsxtminok
	addk	8,a14			;Max
	cmp	a14,a2
	jrle	iqsxtmaxok
	move	a14,a2
iqsxtmaxok
iqsxdone
	move	a2,*a3(plyr_d_skill)

	dsj	b0,iqsxlp


	PULL	a2,a3,a4,a5,a6
	rets


iqsxadj_t	.word	-5,-5,-5,-5,-5
	.word	-5,-5,-5,-4,-3
	.word	-2,-1,-1,0,0
	.word	0
	.word	0,1,1,2,2
	.word	3,3,4,4,5
	.word	5,6,6,6,7

iqsxmin_t	.byte	-15,-12,-10, -9,-8,-7, -7,-6,-6, -6,-6,-6

TMDIFF	.macro
	.byte	-10,-9,-8, -8,-7,-7, -6,-6,-5, -5,-5,-5
	.endm
iqsxtdmin_t
	.byte	-6,-5,-5, -4,-3,-3, -2,-2,-2, -1, 0,-1	;ATL (11)
	TMDIFF						;BOS
	.byte	-7,-7,-6, -6,-5,-5, -4,-4,-3, -3,-2,-3	;CHA (14)
	.byte	6,7,7, 8,9,9, 10,10,11, 11,13,12	;CHI (1)
	.byte	-6,-5,-5, -4,-3,-3, -2,-2,-2, -1, 0,-1	;CLE (10)
	TMDIFF						;DAL
	.byte	-9,-8,-8, -7,-7,-6, -6,-5,-4, -4,-3,-4	;DEN (20)
	.byte	-6,-5,-5, -4,-3,-3, -2,-2,-2, -2,-1,-2	;DET (12)
	.byte	-9,-8,-8, -7,-7,-6, -6,-5,-4, -4,-3,-4	;GOL (18)
	.byte	-1,-1,0, 1,1,1, 2,2,2, 3,4,3		;HOU (6)
	.byte	-4,-3,-3, -2,-1,-1, 0,0,0, 1,2,1	;IND (8)
	TMDIFF						;LAC
	.byte	-5,-4,-4, -3,-2,-2, -1,-1,-1, 0,1,0	;LAL (9)
	.byte	-8,-8,-7, -7,-6,-6, -5,-4,-3, -3,-2,-3	;MI  (17)
	TMDIFF						;MIL
	TMDIFF						;MIN
	TMDIFF						;NJ
	.byte	-3,-2,-2, -1,0,0, 1,1,1, 2,3,2		;NY  (7)
	.byte	4,4,4, 4,4,5, 5,5,7, 7,8,7		;ORL (3)
	TMDIFF						;PHI
	.byte	-8,-8,-7, -7,-6,-6, -5,-4,-3, -3,-2,-3	;PHX (16)
	.byte	-7,-7,-6, -6,-5,-5, -4,-4,-3, -3,-2,-3	;POR (15)
	.byte	-6,-5,-5, -4,-3,-3, -3,-2,-2, -2,-1,-2	;SAC (13)
	.byte	1,1,1, 2,2,2, 3,3,3, 4,5,4		;SAN (5)
	.byte	4,4,5, 5,6,6, 7,7,7, 7,9,8		;SEA (2)
	TMDIFF						;TOR
	.byte	2,2,2, 3,3,3, 4,4,4, 5,6,5		;UTA (4)
	TMDIFF						;VAN
	.byte	-9,-8,-8, -7,-7,-6, -6,-5,-4, -4,-3,-4	;WAS (19)
	.even



 SUBRP	rnd

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	and	a1,a0
	rets



 SUBRP	rndrng0

	move	@RAND,a1,L
	rl	a1,a1
	move	@HCOUNT,a14
	rl	a14,a1
	add	sp,a1
	move	a1,@RAND,L

	addk	1,a0
	mpyu	a1,a0		;Condition codes not valid!

	rets




	.end


